'******************************
'*  VGA 640 x 480 x 8bpp-lut  *
'******************************

CON
  VGA_BUF       = $11000
  PALETTE       = VGA_BUF - 16*4
  BMP_HEADER	= PALETTE - $36
  VERT_BLANK	= $10000

  intensity	= 80	'0..128

  fclk		= 80_000_000.0
  fpix		= 25_000_000.0
  fset		= (fpix / fclk * 2.0) * float($4000_0000)

  vsync		=	0	'vsync pin (all FPGA boards now)

DAT

		org	0
vga_start
'
'
' Setup
'
		rdfast	#0,##PALETTE		'load .bmp palette into lut
		rep	@.end,#$10
		rflong	y
		shl	y,#8
		wrlut	y,x
		add	x,#1
.end
		rdfast	##640*480/128,##VGA_BUF	'set rdfast to wrap on bitmap

		setxfrq ##round(fset)		'set transfer frequency to 25MHz

		'the next 4 lines may be commented out to bypass level scaling

		setcy	##intensity << 24	'r	set colorspace for rgb
		setci	##intensity << 16	'g
		setcq	##intensity << 08	'b
		setcmod	#%01_0_000_0		'enable colorspace conversion

		wrpin	dacmode,#0		'enable dac modes in pins 0..3
		wrpin	dacmode,#1
		wrpin	dacmode,#2
		wrpin	dacmode,#3
'
'
' Field loop
'
field		mov	x,#33			'top blanks
		call	#blank

		wrlong	#0,##VERT_BLANK
		mov     x,#480			'set visible lines
line		call	#hsync			'do horizontal sync
		xcont	m_rf,#4			'visible line
		djnz    x,#line           	'another line?

		wrlong	#1,##VERT_BLANK

		mov	x,#10			'bottom blanks
		call	#blank

		drvnot	#vsync			'sync on

		mov	x,#2			'sync blanks
		call	#blank

		drvnot	#vsync			'sync off

                jmp     #field                  'loop
'
'
' Subroutines
'
blank		call	#hsync			'blank lines
		xcont	m_vi,#0
	_ret_	djnz	x,#blank

hsync		xcont	m_bs,#0			'horizontal sync
		xcont	m_sn,#1
	_ret_	xcont	m_bv,#0
'
'
' Initialized data
'
'dacmode		long	%0000_0000_000_1010000000000_01_00000_0
dacmode		long	%0000_0000_000_1010000000000_01_00000_0

m_bs		long	$CF000000+16		'before sync
m_sn		long	$CF000000+96		'sync
m_bv		long	$CF000000+48		'before visible
m_vi		long	$CF000000+640		'visible

m_rf		long	$6F000000+640		'visible rlong 8bpp lut

x		res	1
y		res	1
'
'
' Bitmap
'
		'orgh	$1000 - 16*4 - $36	'justify pixels at $1000, pallete at $1000-$400
		'file	"lines.bmp"	'640 x 480, 4pbb-lut
